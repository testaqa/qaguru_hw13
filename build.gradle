plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
    mavenCentral()
}

def allureVersion = "2.13.8",
    selenideVersion = "5.19.0",
    junitVersion = "5.7.0",
    ownerVersion = "1.0.12",
    allureTestopsVersion = "3.28.2"

dependencies {
    implementation("org.aeonbits.owner:owner:${ownerVersion}")
    testImplementation(
            "com.codeborne:selenide:${selenideVersion}",
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
            "io.qameta.allure:allure-selenide:${allureVersion}",
            "io.qameta.allure:allure-ee-junit-platform:${allureTestopsVersion}",
            "org.aspectj:aspectjweaver:1.9.6",
            "org.junit.jupiter:junit-jupiter-params:${junitVersion}")
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}

tasks.withType(Test) {
    systemProperties += System.properties
    systemProperty("junit.jupiter.extensions.autodetection.enabled", "true")

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }
}

test {
    useJUnitPlatform()
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
    systemProperty 'platform', 'web'
}